apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'com.jeskeshouse'
version '2.0.0-SNAPSHOT'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'com.jfrog.bintray'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'com.android.tools.build:gradle:0.14.0'
    compile gradleApi()
}

task wrapper(type: Wrapper) {
    gradleVersion '2.1'
}

bintray {
    user = bintrayUserName
    key = bintrayApiKey
    publish = true
    publications = ['mavenJava']

    pkg {
        repo = 'android-utils'
        name = 'robolectric-androidstudio-plugin'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

def pomConfig = {
    licenses {
        license {
            name 'The MIT License'
            url 'http://www.opensource.org/licenses/mit-license.php'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id "jeske717"
            name "Ben Jeske"
            email "jeske717@gmail.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
               def root = asNode()
               root.children().last() + pomConfig 
            }
        }
    }
}
